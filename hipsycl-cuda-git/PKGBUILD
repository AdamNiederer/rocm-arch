# Maintainer: Aksel Alpay <aksel.alpay at uni-heidelberg dot de>
pkgname=hipsycl-cuda-git
_pkgname=hipSYCL
pkgver=latest
pkgrel=1
pkgdesc="SYCL implementation over CUDA/HIP for NVIDIA devices."
arch=('x86_64')
url="https://github.com/illuhad/hipSYCL"
license=('BSD')
depends=('cuda-9.2' 'llvm' 'clang' 'boost' 'openmp')
makedepends=('cmake')
provides=('hipSYCL' 'SYCL')
install="$pkgname.install"
source=("git+https://github.com/illuhad/hipSYCL.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$_pkgname/contrib/HIP"
  git submodule init
  git submodule update
}

build() {
  mkdir -p "$srcdir/$_pkgname/build"
  cd "$srcdir/$_pkgname/build"
  # We compile with nvcc since Arch Linux uses very new versions of CUDA
  # which may not yet be supported by clang.
  CXX=clang++ cmake -DCMAKE_BUILD_TYPE=Release \
                    -DCMAKE_INSTALL_PREFIX=/opt/hipSYCL/CUDA \
                    -DWITH_CPU_BACKEND=ON \
                    -DWITH_CUDA_BACKEND=ON \
                    -DCMAKE_CXX_FLAGS="--cuda-path=/opt/cuda-9.2 -L/opt/cuda-9.2/lib64" \
                    ..
}

package() {
  cd "$srcdir/$_pkgname/build"
  VERBOSE=1 make DESTDIR="$pkgdir" install
}
