# Maintainer: acxz <akashpatel2008 at yahoo dot com>
# Contributor: Jakub Oko≈Ñski <jakub@okonski.org>
pkgname=rocr-runtime
pkgver=3.3.0
pkgrel=2
pkgdesc="ROCm Platform Runtime: ROCr a HPC market enhanced HSA based runtime"
arch=('x86_64')
url="https://github.com/RadeonOpenCompute/ROCR-Runtime"
license=('custom:NCSAOSL')
makedepends=('cmake' 'libelf' 'hsakmt-roct')
source=("https://github.com/RadeonOpenCompute/ROCR-Runtime/archive/rocm-$pkgver.tar.gz")
sha256sums=('fa2d2d1f8a61d8a6952d377cf288d78c61776c3c2a666f163cafc3aa19ab0b61')

build() {
  mkdir -p "$srcdir/build"
  cd "$srcdir/build"

  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/opt/rocm \
        -DHSAKMT_INC_PATH=/opt/rocm/include \
        -DHSAKMT_LIB_PATH=/opt/rocm/lib \
        "$srcdir/ROCR-Runtime-rocm-$pkgver/src"
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/etc/ld.so.conf.d"
  cat << EOF > "$pkgdir/etc/ld.so.conf.d/rocm-runtime.conf"
/opt/rocm/lib
/opt/rocm/hsa/lib
EOF
}
