# Maintainer: Aksel Alpay <aksel.alpay at uni-heidelberg dot de>
pkgname=hipsycl-rocm-git
_pkgname=hipSYCL
pkgver=latest
pkgrel=1
pkgdesc="SYCL implementation over CUDA/HIP for AMD devices."
arch=('x86_64')
url="https://github.com/illuhad/hipSYCL"
license=('BSD')
depends=('git' 'llvm' 'clang' 'boost' 'openmp')
makedepends=('cmake')
provides=('hipSYCL' 'SYCL')
source=("git+https://github.com/illuhad/hipSYCL.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$_pkgname/contrib/HIP"
  git submodule init
  git submodule update
}

build() {
    mkdir -p "$srcdir/$_pkgname/build"
    cd "$srcdir/$_pkgname/build"

    cmake -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/opt/hipSYCL/ROCm \
          -DWITH_CPU_BACKEND=ON \
          -DWITH_ROCM_BACKEND=ON \
          ..
    make
}

package() {
    cd "$srcdir/$_pkgname/build"

    VERBOSE=1 make DESTDIR="$pkgdir" install
}
